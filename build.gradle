plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "sealparticleplayer" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

repositories {
    maven {url = 'https://maven.blamejared.com/' }
    maven {url = "https://mvn.devos.one/releases/" }
    maven {url = "https://api.modrinth.com/maven" }
    maven {url = "https://maven.theillusivec4.top/"}
    maven {url = "https://maven.jamieswhiteshirt.com/libs-release"}
    maven {url = 'https://maven.ladysnake.org/releases'}
    maven {url = "https://maven.terraformersmc.com/"}
    maven { url 'https://jitpack.io' }
    if (project.useLocalRepoForEKC) {
        mavenLocal()
    }
    mavenCentral()
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation("team.lodestar.lodestone:lodestone:${project.minecraft_version}-${project.lodestone_version}-fabric")
    //For this you also need the following
    modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}")
    modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}")
    modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cca_version}")

    modImplementation("dev.emi:trinkets:${project.trinkets_version}")
    for (String module in port_lib_modules.split(",")) {
        modImplementation("io.github.fabricators_of_create.Porting-Lib:$module:$port_lib_version")
    }
    modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}")

    //implementation("dev.iseal:ExtraKryoCodecs:${project.ekc_version}")
    // use this when dev ^^

    implementation("com.github.ISeal-plugin-developement:ExtraKryoCodecs:${project.ekc_version}")
}

shadowJar {

    archiveFileName = "${project.file_name}-${project.mod_version}.jar"

    from remapJar.outputs

    // Include only the specific library in the final JAR
    dependencies {
        //include(dependency("dev.iseal:ExtraKryoCodecs:${project.ekc_version}"))
        // use this when dev ^^

        include(dependency("com.github.ISeal-plugin-developement:ExtraKryoCodecs:${project.ekc_version}"))
        //TODO: make this better. it fucking sucks.
        include(dependency("com.esotericsoftware.kryo:kryo5:5.6.2"))
    }
    relocate "dev.iseal.ExtraKryoCodecs", "libs.dev.iseal.ExtraKryoCodecs"
}

jar.finalizedBy shadowJar

//build.dependsOn remapJar
//jar.dependsOn shadowJar

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "lodestone_version", project.lodestone_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "lodestone_version": project.lodestone_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}